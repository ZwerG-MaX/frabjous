# Copyright 1999-2018 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2

EAPI=6

inherit golang-vcs-snapshot systemd user

GIT_COMMIT="abb143a"
EGO_PN="github.com/prometheus/${PN/prometheus-}"
DESCRIPTION="An exporter that exposes information gathered from SNMP for Prometheus"
HOMEPAGE="https://prometheus.io"
SRC_URI="https://${EGO_PN}/archive/v${PV}.tar.gz -> ${P}.tar.gz"
RESTRICT="mirror strip"

LICENSE="Apache-2.0"
SLOT="0"
KEYWORDS="~amd64"
IUSE="test"

DEPEND="test? ( net-analyzer/net-snmp )"

DOCS=( NOTICE README.md )

G="${WORKDIR}/${P}"
S="${G}/src/${EGO_PN}"

pkg_setup() {
	if use test; then
		has network-sandbox $FEATURES && \
			die "The test phase requires 'network-sandbox' to be disabled in FEATURES"
	fi

	enewgroup snmp_exporter
	enewuser snmp_exporter -1 -1 -1 snmp_exporter
}

src_compile() {
	export GOPATH="${G}"
	local GOLDFLAGS="-s -w
		-X ${EGO_PN}/vendor/${EGO_PN%/*}/common/version.Version=${PV}
		-X ${EGO_PN}/vendor/${EGO_PN%/*}/common/version.Revision=${GIT_COMMIT}
		-X ${EGO_PN}/vendor/${EGO_PN%/*}/common/version.BuildUser=$(id -un)@$(hostname -f)
		-X ${EGO_PN}/vendor/${EGO_PN%/*}/common/version.Branch=non-git
		-X ${EGO_PN}/vendor/${EGO_PN%/*}/common/version.BuildDate=$(date -u '+%Y%m%d-%I:%M:%S')"

	go build -v -ldflags \
		"${GOLDFLAGS}" || die
}

src_test() {
	go test -v ./... || die
}

src_install() {
	dobin snmp_exporter
	einstalldocs

	newinitd "${FILESDIR}"/${PN}.initd ${PN}
	newconfd "${FILESDIR}"/${PN}.confd ${PN}
	systemd_dounit "${FILESDIR}"/${PN}.service

	insinto /etc/snmp_exporter
	newins snmp.yml snmp.yml.example

	diropts -o snmp_exporter -g snmp_exporter -m 0750
	keepdir /var/log/snmp_exporter
}

pkg_postinst() {
	if [ ! -e "${EROOT%/}"/etc/snmp_exporter/snmp.yml ]; then
		elog "No snmp.yml found, copying the example over"
		cp "${EROOT%/}"/etc/snmp_exporter/snmp.yml{.example,} || die
	else
		elog "snmp.yml found, please check example file for possible changes"
	fi
}
